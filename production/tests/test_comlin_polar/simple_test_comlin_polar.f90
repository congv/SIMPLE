program simple_test_comlin_polar
include 'simple_lib.f08'
use simple_cmdline,          only: cmdline
use simple_parameters,       only: parameters
use simple_image,            only: image
use simple_projector,        only: projector
use simple_comlin,           only: comlin_map, polar_comlin_map
use simple_polarft_corrcalc, only: polarft_corrcalc
use simple_polarizer,        only: polarizer
implicit none
integer,          parameter   :: NPLANES = 20, ORI_IND1 = 10, ORI_IND2 = 1, NTHETAS = 18
character(len=:), allocatable :: cmd
type(fplan_map),  allocatable :: all_coords(:)
type(polar_fmap), allocatable :: polar_coords(:)
type(image),      allocatable :: pad_fplanes(:)
real,             allocatable :: thetas(:)
complex,          allocatable :: cmat(:,:), pft(:,:), cur_pft(:,:)
type(fplan_map)               :: coord_map(NPLANES)
type(polar_fmap)              :: polar_map(NPLANES)
type(parameters)              :: p
type(polarft_corrcalc)        :: pftcc
type(polarizer)               :: img_polarizer
type(cmdline)                 :: cline
type(image)                   :: noise, ptcl, ptcl_pad, fplanes(NPLANES), vol, img, fplane_polar
type(oris)                    :: spiral
type(ori)                     :: o1, o2
type(projector)               :: vol_pad
integer :: ifoo, rc, i, j, ori_phys(3), tar_phys(3), f_ind, lims(3,2), ithr, box, pdim(3), ndim, ori_pcoord(2), tar_pcoord(2),&
          &irot, kind, errflg
real    :: ave, sdev, maxv, minv, hk(2), e1_rotmat(3,3), loc1_3D(3), loc2_3D(3), denom, a1, a2, b1, b2, line_xyz(3),&
          &irot_real, e1_inv(3,3), k_real
complex :: val
logical :: mrc_exists

if( command_argument_count() < 4 )then
    write(logfhandle,'(a)') 'ERROR! Usage: simple_test_comlin_polar smpd=xx nthr=yy vol1=volume.mrc mskdiam=zz'
    write(logfhandle,'(a)') 'Example: https://www.rcsb.org/structure/1jyx with smpd=1. mskdiam=180'
    write(logfhandle,'(a)') 'DEFAULT TEST (example above) is running now...'
    inquire(file="1JYX.mrc", exist=mrc_exists)
    if( .not. mrc_exists )then
        write(*, *) 'Downloading the example dataset...'
        cmd = 'curl -s -o 1JYX.pdb https://files.rcsb.org/download/1JYX.pdb'
        call execute_command_line(cmd, exitstat=rc)
        write(*, *) 'Converting .pdb to .mrc...'
        cmd = 'e2pdb2mrc.py 1JYX.pdb 1JYX.mrc'
        call execute_command_line(cmd, exitstat=rc)
        cmd = 'rm 1JYX.pdb'
        call execute_command_line(cmd, exitstat=rc)
    endif
    call cline%set('smpd'   , 1.)
    call cline%set('nthr'   , 16.)
    call cline%set('vol1'   , '1JYX.mrc')
    call cline%set('mskdiam', 180.)
    call cline%set('lp'   ,   3.)
else
    call cline%parse_oldschool
endif
call cline%checkvar('smpd',    1)
call cline%checkvar('nthr',    2)
call cline%checkvar('vol1',    3)
call cline%checkvar('mskdiam', 4)
call cline%checkvar('lp',      5)
call cline%check
call p%new(cline)
call find_ldim_nptcls(p%vols(1), p%ldim, ifoo)
call vol%new(p%ldim, p%smpd)
call vol%read(p%vols(1))
call vol%stats('foreground', ave, sdev, maxv, minv)
call spiral%new(NPLANES, is_ptcl=.false.)
call spiral%spiral(no_ends=.true.)
call ptcl%new(    [p%box,   p%box,   1],       p%smpd)
call noise%new(   [p%box,   p%box ,  1],       p%smpd)
call img%new(     [p%box,   p%box,   1],       p%smpd)
call vol_pad%new( [p%boxpd, p%boxpd, p%boxpd], p%smpd)
call ptcl_pad%new([p%boxpd, p%boxpd, 1],       p%smpd)
call vol%pad(vol_pad)
call vol_pad%fft
call vol_pad%expand_cmat(p%alpha)
call spiral%get_ori(ORI_IND1, o1)
call vol_pad%fproject(o1,ptcl_pad)
call ptcl_pad%ifft
! add noise in a small center region of the vol
call noise%gauran(0., 0.01 * sdev)
call noise%mask(p%msk, 'soft')
call ptcl_pad%clip(ptcl)
call ptcl%add(noise)
call ptcl%write('reproj_com_reprojcom.mrc', 1)
call ptcl%fft
lims = vol%loop_lims(3)
ndim = (lims(1,2)-lims(1,1)+1)*(lims(2,2)-lims(2,1)+1)
allocate(pad_fplanes(p%nthr),all_coords(p%nthr))
do ithr = 1, p%nthr
    call pad_fplanes(ithr)%new([p%boxpd, p%boxpd, 1], p%smpd)
    allocate(all_coords(ithr)%tar_find(  ndim),&
            &all_coords(ithr)%ori_phys(3,ndim),&
            &all_coords(ithr)%ori_four(2,ndim),&
            &all_coords(ithr)%tar_phys(3,ndim),&
            &all_coords(ithr)%tar_four(2,ndim))
enddo
!$omp parallel do default(shared) private(i,ithr,o2)&
!$omp proc_bind(close) schedule(static)
do i = 1, spiral%get_noris()
    ithr = omp_get_thread_num() + 1
    ! common line mapping is independent of fplanes below
    call comlin_map(lims, i, spiral, coord_map(i), all_coords(ithr))
    ! print *, 'i = ', i, '; cart npoints = ', coord_map(i)%n_points
    ! fplanes are used for optimization below
    call spiral%get_ori(i, o2)
    call fplanes(i)%new([p%box, p%box, 1], p%smpd)
    call vol_pad%fproject(o2,pad_fplanes(ithr))
    call pad_fplanes(ithr)%ifft
    call pad_fplanes(ithr)%clip(fplanes(i))
enddo
!$omp end parallel do
do i = 1, spiral%get_noris()
    call fplanes(i)%write('fplanes.mrc', i)
    call fplanes(i)%fft
enddo
! images generated by common lines only
i = ORI_IND1
call img%zero_and_flag_ft
do j = 1, coord_map(i)%n_points
    f_ind    = coord_map(i)%tar_find(j)
    ori_phys = coord_map(i)%ori_phys(:,j)
    tar_phys = coord_map(i)%tar_phys(:,j)
    if( .not.(check_kfromto(coord_map(i)%ori_four(1:2,j)) .and. check_kfromto(coord_map(i)%tar_four(1:2,j))) ) cycle
    val      = img%get_cmat_at(ori_phys)
    call img%set_cmat_at(ori_phys, val + fplanes(f_ind)%get_cmat_at(tar_phys))
enddo
call img%ifft
call img%write('reproj_com_reprojcom.mrc', 2)
! noisy img + img from common lines
call img%zero_and_flag_ft
call img%set_cmat(ptcl%get_cmat())
do j = 1, coord_map(i)%n_points
    f_ind    = coord_map(i)%tar_find(j)
    ori_phys = coord_map(i)%ori_phys(:,j)
    tar_phys = coord_map(i)%tar_phys(:,j)
    if( .not.(check_kfromto(coord_map(i)%ori_four(1:2,j)) .and. check_kfromto(coord_map(i)%tar_four(1:2,j))) ) cycle
    val      = img%get_cmat_at(ori_phys)
    call img%set_cmat_at(ori_phys, val + fplanes(f_ind)%get_cmat_at(tar_phys))
enddo
call img%ifft
call img%write('reproj_com_reprojcom.mrc', 3)
! polar stuffs
call img_polarizer%new([p%box,p%box,1],p%smpd, wthreads=.false.)
call pftcc%new(NPLANES, [1,NPLANES], p%kfromto)
call img_polarizer%init_polarizer(pftcc, p%alpha)
call fplane_polar%new([p%box,p%box,1],1.0)
do i = 1, spiral%get_noris()
    call img_polarizer%polarize(pftcc, fplanes(i), i, isptcl=.false., iseven=.true.)
    call pftcc%polar2cartesian(i, .true., cmat, box, box_in=p%box)
    call fplane_polar%fft
    call fplane_polar%set_cmat(cmat)
    call fplane_polar%shift_phorig()
    call fplane_polar%ifft
    call fplane_polar%write('fplanes_polar.mrc', i)
enddo
pdim = pftcc%get_pdim()
! print *, 'pdim = ', pdim
ndim = pdim(1) * 2 * (pdim(3) - pdim(2))
allocate(polar_coords(p%nthr))
do ithr = 1, p%nthr
    allocate(polar_coords(ithr)%tar_find(  ndim),&
            &polar_coords(ithr)%ori_inds(2,ndim),&
            &polar_coords(ithr)%tar_inds(2,ndim))
enddo
!$omp parallel do default(shared) private(i,ithr)&
!$omp proc_bind(close) schedule(static)
do i = 1, spiral%get_noris()
    ithr = omp_get_thread_num() + 1
    ! common line mapping is independent of fplanes below
    call polar_comlin_map(lims, i, spiral, pftcc, polar_map(i), polar_coords(ithr))
    ! print *, 'i = ', i, '; npoints = ', polar_map(i)%n_points
enddo
!$omp end parallel do
! images generated by polar common lines only
i   = ORI_IND1
pft = pftcc%get_ref_pft( i, iseven=.true. )
pft = cmplx(0.,0.)
do j = 1, polar_map(i)%n_points
    f_ind      = polar_map(i)%tar_find(j)
    if( f_ind == i )cycle
    cur_pft    = pftcc%get_ref_pft( f_ind, iseven=.true. )
    ori_pcoord = polar_map(i)%ori_inds(:,j)
    tar_pcoord = polar_map(i)%tar_inds(:,j)
    pft(ori_pcoord(1), ori_pcoord(2)) = pft(ori_pcoord(1), ori_pcoord(2)) + cur_pft(tar_pcoord(1), tar_pcoord(2))
enddo
call pftcc%set_ref_pft(i, pft, iseven=.true.)
call pftcc%polar2cartesian(i, .true., cmat, box, box_in=p%box)
call fplane_polar%fft
call fplane_polar%set_cmat(cmat)
call fplane_polar%shift_phorig()
call fplane_polar%ifft
call fplane_polar%write('polar_comlin.mrc', 1)
! Testing polar line generation
e1_rotmat = o1%get_mat()
irot      = 5
kind      = p%kfromto(1) + 2
hk        = pftcc%get_coord(irot,kind)
loc1_3D   = matmul([hk(1), hk(2), 0.], e1_rotmat)
irot      = 16
kind      = p%kfromto(1) + 7
hk        = pftcc%get_coord(irot,kind)
loc2_3D   = matmul([hk(1), hk(2), 0.], e1_rotmat)
denom     = (loc1_3D(1) * loc2_3D(2) - loc1_3D(2) * loc2_3D(1))
a1        = (loc1_3D(3) * loc2_3D(2) - loc1_3D(2) * loc2_3D(3)) / denom
b1        = (loc1_3D(1) * loc2_3D(3) - loc1_3D(3) * loc2_3D(1)) / denom
print *, 'a1 = ', a1
print *, 'b1 = ', b1
call spiral%get_ori(ORI_IND2, o2)
e1_rotmat = o2%get_mat()
irot      = 5
kind      = p%kfromto(1) + 2
hk        = pftcc%get_coord(irot,kind)
loc1_3D   = matmul([hk(1), hk(2), 0.], e1_rotmat)
irot      = 16
kind      = p%kfromto(1) + 7
hk        = pftcc%get_coord(irot,kind)
loc2_3D   = matmul([hk(1), hk(2), 0.], e1_rotmat)
denom     = (loc1_3D(1) * loc2_3D(2) - loc1_3D(2) * loc2_3D(1))
a2        = (loc1_3D(3) * loc2_3D(2) - loc1_3D(2) * loc2_3D(3)) / denom
b2        = (loc1_3D(1) * loc2_3D(3) - loc1_3D(3) * loc2_3D(1)) / denom
print *, 'a2 = ', a2
print *, 'b2 = ', b2
!
line_xyz(1:2) = [1., -(a1-a2)/(b1-b2)]
line_xyz(3)   = a1*line_xyz(1) + b1*line_xyz(2)
e1_rotmat     = o1%get_mat()
call matinv(e1_rotmat, e1_inv, 3, errflg)
line_xyz      = matmul(line_xyz, e1_inv)
print *, 'line_xyz  1 = ', line_xyz
call pftcc%get_polar_coord(line_xyz(1:2), irot_real, k_real)
if( line_xyz(1) < 0. ) irot_real = irot_real + pftcc%get_pftsz()
print *, 'irot_real 1 = ', irot_real
!
line_xyz(1:2) = [1., -(a1-a2)/(b1-b2)]
line_xyz(3)   = a2*line_xyz(1) + b2*line_xyz(2)
e1_rotmat     = o2%get_mat()
call matinv(e1_rotmat, e1_inv, 3, errflg)
line_xyz      = matmul(line_xyz, e1_inv)
print *, 'line_xyz  2 = ', line_xyz
call pftcc%get_polar_coord(line_xyz(1:2), irot_real, k_real)
if( line_xyz(1) < 0. ) irot_real = irot_real + pftcc%get_pftsz()
print *, 'irot_real 2 = ', irot_real

contains

    function check_kfromto(addr) result(okay)
        integer, intent(in) :: addr(2)
        logical :: okay
        integer :: sh
        sh   = nint(hyp(addr(1),addr(2)))
        okay = .false.
        if( sh >= p%kfromto(1) .and. sh <= p%kfromto(2) ) okay = .true.
    end function check_kfromto

end program simple_test_comlin_polar